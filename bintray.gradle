apply plugin: 'com.jfrog.bintray'

if (project.hasProperty("android")) {
    // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else {
    // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    if (project.hasProperty('bintrayUser')) {
        user = findProperty('bintrayUser')
    } else {
        println("[WARNING] No bintrayUser provided")
    }
    if (project.hasProperty('bintrayKey')) {
        key = bintrayKey
    } else {
        println("[WARNING] No bintrayKey provided")
    }
    if (project.hasProperty('publishVersion')) {
        version = findProperty('publishVersion')
    } else {
        println("[WARNING] No publishVersion provided")
    }

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        dryRun = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
            }
        }
    }
}
